// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        minSdkVersion = 21
        targetSdkVersion = 30
        compileSdkVersion = 30
        kotlinJDKVersion = "8"
        gradleBuildVersion = '4.2.2'
        kotlin_version = "1.4.21"
        lifecycleExtensionVersion = "2.0.0"
        mockkVersion = "1.10.0"
        amityMessagingSdkVersion = '5.10.0'
        amityRxLifeCycleVersion = '1.1.1'

        jacocoVersion = '0.8.5'
        exoplayerVersion = '2.12.1'
        // Will contain all support library dependencies
        sharedSupportLibraries = { dependencyHandler ->
            delegate = dependencyHandler

            implementation 'androidx.appcompat:appcompat:1.3.0'
            implementation 'androidx.core:core-ktx:1.5.0'
            implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
            implementation 'com.google.android.material:material:1.3.0'
            implementation 'androidx.paging:paging-runtime:2.1.2'
            implementation "androidx.activity:activity-ktx:1.2.3"
            implementation "androidx.fragment:fragment-ktx:1.3.4"


        }
    }
    repositories {
        google()
        maven { url 'https://jitpack.io' }
        jcenter()
        maven {
            url 'https://maven.google.com/'
        }
        maven {
            url "https://repo.maven.apache.org/maven2/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${gradleBuildVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jacoco:org.jacoco.core:$jacocoVersion"
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'gradle.plugin.com.github.sgtsilvio.gradle:android-retrofix:0.4.1'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        maven { url 'https://jitpack.io' }
        jcenter()
        maven {
            url "https://repo.maven.apache.org/maven2/"
        }
    }
}

apply plugin: 'jacoco'
task jacocoRootReport(type: JacocoReport, dependsOn: ['chat:jacocoTestReport', 'common:jacocoTestReport', 'social:jacocoTestReport']) {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    sourceDirectories.from = files([tasks.getByPath("chat:jacocoTestReport").sourceDirectories,
                                    tasks.getByPath("common:jacocoTestReport").sourceDirectories,
                                    tasks.getByPath("social:jacocoTestReport").sourceDirectories])

    classDirectories.from = files([tasks.getByPath("chat:jacocoTestReport").classDirectories,
                                   tasks.getByPath("common:jacocoTestReport").classDirectories,
                                   tasks.getByPath("social:jacocoTestReport").classDirectories])

    executionData.from = files([tasks.getByPath("chat:jacocoTestReport").executionData,
                                tasks.getByPath("common:jacocoTestReport").executionData,
                                tasks.getByPath("social:jacocoTestReport").executionData])

}

task clean(type: Delete) {
    delete rootProject.buildDir
}
